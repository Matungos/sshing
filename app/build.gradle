apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

repositories {
    google()
    mavenCentral()
}

def keystoreDebugProperties = new Properties()
def keystoreDebugPropertiesFile = rootProject.file('key.debug.properties')
if (keystoreDebugPropertiesFile.exists()) {
    keystoreDebugProperties.load(new FileInputStream(keystoreDebugPropertiesFile))
}

def keystoreReleaseProperties = new Properties()
def keystoreReleasePropertiesFile = rootProject.file('key.release.properties')
if (keystoreReleasePropertiesFile.exists()) {
    keystoreReleaseProperties.load(new FileInputStream(keystoreReleasePropertiesFile))
}

android {
    compileSdkVersion setup.compileSdk

    sourceSets { main.java.srcDirs += 'src/main/kotlin' }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'com.matungos.sshing'
        minSdkVersion setup.minSdk
        targetSdkVersion setup.targetSdk
        versionCode release.versionCode
        versionName release.versionName
        multiDexEnabled true
        archivesBaseName = "sshing-" + versionName
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            buildConfigField "boolean", "LOG_ENABLED", "true"
            buildConfigField "boolean", "FIREBASE_ENABLED", "false"
            buildConfigField "boolean", "APPCENTER_ENABLED", "false"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile 'proguard-conceal.pro'
            proguardFile 'proguard-gson.pro'
            proguardFile 'proguard-jsch.pro'
            proguardFile 'proguard-okhttp.pro'
            proguardFile 'proguard-rules.pro'
            proguardFile 'proguard-sshj.pro'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            buildConfigField "boolean", "LOG_ENABLED", "false"
            buildConfigField "boolean", "FIREBASE_ENABLED", "true"
            buildConfigField "boolean", "APPCENTER_ENABLED", "true"
        }
    }

    signingConfigs {
        debug {
            keyAlias keystoreDebugProperties['keyAlias']
            keyPassword keystoreDebugProperties['keyPassword']
            storeFile file(keystoreDebugProperties['storeFile'])
            storePassword keystoreDebugProperties['storePassword']
        }
        release {
            keyAlias keystoreReleaseProperties['keyAlias']
            keyPassword keystoreReleaseProperties['keyPassword']
            storeFile file(keystoreReleaseProperties['storeFile'])
            storePassword keystoreReleaseProperties['storePassword']
        }
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            dimension "default"
            applicationId 'com.matungos.sshing.dev'
            signingConfig signingConfigs.debug
            buildConfigField "boolean", "IS_FREE_VERSION", "true"
        }
        free {
            dimension "default"
            applicationId 'com.matungos.sshing.free'
            signingConfig signingConfigs.release
            buildConfigField "boolean", "IS_FREE_VERSION", "true"
        }
        donate {
            dimension "default"
            applicationId 'com.matungos.sshing.donate'
            signingConfig signingConfigs.release
            buildConfigField "boolean", "IS_FREE_VERSION", "false"
        }
    }

    packagingOptions {
        pickFirst 'META-INF/atomicfu.kotlin_module'
    }
}

dependencies {
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.fragment:fragment-ktx:1.3.3'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.0.1'
    implementation 'com.google.firebase:firebase-analytics:19.0.0'
    implementation 'com.google.code.gson:gson:2.8.7'


    //Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:color:3.3.0'
    implementation 'com.afollestad.material-dialogs:files:3.3.0'

    implementation 'com.jcraft:jsch:0.1.55'

    implementation 'com.hierynomus:sshj:0.31.0'

    implementation "org.bouncycastle:bcprov-jdk15on:1.68"

    implementation 'com.facebook.conceal:conceal:1.1.3@aar'

    def appCenterSdkVersion = '4.2.0'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
}

// gradlew.bat dependencyUpdates -Drevision=release
// ./gradlew dependencyUpdates -Drevision=release
